plugins {
    id 'java'
    id 'idea'
    id "jacoco"
    id "com.github.johnrengelman.shadow" version "4.0.4"
}

shadowJar {
    baseName = this.name
    version = "3.0.0"

    manifest {
        attributes 'Implementation-Title' : baseName,
                   'Implementation-Version' : version,
                   'Main-Class' : "com.elypia.alexis.Alexis"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

repositories  {
    jcenter()
    mavenCentral()

    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    // JDAC
    implementation files("/home/seth/Desktop/Elypia/Git/Commandler/export/build/libs/export.jar")
    implementation files("/home/seth/Desktop/Elypia/Git/JDAC/core/build/libs/core.jar")
    implementation files("/home/seth/Desktop/Elypia/Git/JDAC/logback-logging/build/libs/logback-logging.jar")

    // Elypiai
    implementation "com.elypia.elypiai:cleverbot:2.1.2"
    implementation files("/home/seth/Desktop/Elypia/Git/Elypiai/nanowrimo/build/libs/nanowrimo.jar")
    implementation files("/home/seth/Desktop/Elypia/Git/Elypiai/osu/build/libs/osu.jar")
    implementation "com.elypia.elypiai:runescape:2.1.2"
    implementation "com.elypia.elypiai:steam:2.1.2"
    implementation "com.elypia.elypiai:twitch:2.1.2"
    implementation "com.elypia.elypiai:urbandictionary:2.1.2"

    // ElyScript
    implementation "com.elypia.elyscript:sheets:1.0.1"

    // Database
    implementation "org.hibernate:hibernate-core:5.4.1.Final"
    runtimeOnly "mysql:mysql-connector-java:8.0.15"

    // Config
    implementation "com.electronwill.night-config:core:3.4.2"
    runtimeOnly "com.electronwill.night-config:toml:3.4.2"

    // Discord
    implementation "net.dv8tion:JDA:4.ALPHA.0_45"
    implementation "com.github.FredBoat:Lavalink-Client:3.0"
    implementation "com.github.DiscordBotList:DBL-Java-Library:2.0.1"

    // Google
    implementation "com.google.cloud:google-cloud-translate:1.62.0"
    implementation "com.google.apis:google-api-services-youtube:v3-rev206-1.25.0"

    // Logging
    implementation "org.slf4j:slf4j-api:1.7.25"
    runtimeOnly "ch.qos.logback:logback-classic:1.2.3"

    // Testing
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.4.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.4.0"
    testRuntimeOnly "org.junit.platform:junit-platform-runner:1.4.0"

    // Mocking
    testImplementation "org.mockito:mockito-core:2.24.0"
}

/**
 * Testing, reporting and coverage for Codacy.
 */

configurations {
    codacy
}

repositories {
    maven {
        url "http://dl.bintray.com/typesafe/maven-releases"
    }
}

dependencies {
    codacy 'com.github.codacy:codacy-coverage-reporter:-SNAPSHOT'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport

    reports {
        html.enabled = false
    }
}

jacoco {
    toolVersion = "0.8.3"
}

jacocoTestReport {
    reports {
        html.enabled = false
        xml.enabled = true
    }
}

task codacyCoverage(type: JavaExec, dependsOn: jacocoTestReport) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy

    args = [
        "report", "-l", "Java", "-r", "${buildDir}/reports/jacoco/jacocoTestReport/jacoco.xml"
    ]
}
