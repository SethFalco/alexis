/*
 * Alexis - A general purpose chatbot for Discord.
 * Copyright (C) 2019-2019  Elypia CIC
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as
 * published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        mavenLocal()
    }

    dependencies {
        classpath 'org.elypia.commandler:commandler:1.2.0'
    }
}

plugins {
    id 'java'
    id 'idea'
    id "jacoco"
    id "com.github.johnrengelman.shadow" version "5.2.0"
}

apply plugin: "org.elypia.commandler"

group = "org.elypia"
version = "3.0.0"
description = "Hey! I'm a chatbot for Discord!"
sourceCompatibility = 11
targetCompatibility = 11

ext {
    comcordVersion = "2.0.0"
    elypiaiVersion = "2.3.0"
    hibernateVersion = "5.4.9.Final"
}

repositories {
    jcenter()
    mavenLocal()
    maven { url "https://oss.jfrog.org/artifactory/libs-release" }
    maven { url 'https://jitpack.io' }
}

dependencies {
    // ComCord (Bundles Java Discord API)
    implementation "org.elypia.comcord:core:${comcordVersion}"
    implementation "org.elypia.comcord:validation:${comcordVersion}"

    // Elypiai
    implementation "org.elypia.elypiai:cleverbot:${elypiaiVersion}"
    implementation "org.elypia.elypiai:osu:${elypiaiVersion}"
    implementation "org.elypia.elypiai:path-of-exile:${elypiaiVersion}"
    implementation "org.elypia.elypiai:runescape:${elypiaiVersion}"
    implementation "org.elypia.elypiai:steam:${elypiaiVersion}"
    implementation "org.elypia.elypiai:urbandictionary:${elypiaiVersion}"
    implementation "org.elypia.elypiai:yugioh-prices:${elypiaiVersion}"

    // Elypiai Extensions TODO: This should be extensions-redis
    implementation "org.elypia.elypiai:redis:${elypiaiVersion}"

    // Discord
    implementation "net.dv8tion:JDA:4.0.0_68"
    implementation "com.sedmelluq:lavaplayer:1.3.29"
    implementation "com.github.DiscordBotList:DBL-Java-Library:2.0.1"

    // Twitch
    implementation "com.github.twitch4j:twitch4j:1.0.0-alpha.17"

    // Google Translate and YouTube
    implementation "com.google.cloud:google-cloud-translate:1.94.1"
    implementation "com.google.apis:google-api-services-youtube:v3-rev212-1.25.0"

    // YAML Configuration for Commandler (Apache Commons Configuration)
    runtimeOnly "org.yaml:snakeyaml:1.25"

    // Database
    implementation "org.hibernate:hibernate-core:${hibernateVersion}"
    implementation "org.hibernate:hibernate-c3p0:${hibernateVersion}"
    runtimeOnly "mysql:mysql-connector-java:8.0.18"

    // Logging
    implementation "org.slf4j:slf4j-api:1.7.29"
    runtimeOnly "org.codehaus.janino:janino:3.1.0"
    runtimeOnly "ch.qos.logback:logback-classic:1.2.3"
    runtimeOnly "com.google.cloud:google-cloud-logging-logback:0.116.0-alpha"

    // Testing
    testImplementation "org.junit.jupiter:junit-jupiter:5.5.2"
    testImplementation "org.mockito:mockito-core:3.2.0"
}

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

shadowJar {
    manifest {
        attributes 'Implementation-Title' : this.name,
                   'Implementation-Version' : version,
                   'Main-Class' : "org.elypia.alexis.Alexis"
    }
}

/** Testing */

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport

    reports {
        html.enabled = false
    }
}

/** Coverage **/

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = false
    }
}
