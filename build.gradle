/*
 * Copyright (C) 2019  Elypia
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as
 * published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'java'
    id 'idea'
    id "jacoco"
    id "com.github.johnrengelman.shadow" version "5.1.0"
//    id "com.elypia.commandler" version "1.0.0" TODO: Doesn't work
}

group = "com.elypia.alexis"
version = "3.0.0"
description = "Hey! I'm a chatbot for Discord!"
sourceCompatibility = 11
targetCompatibility = 11

repositories {
    jcenter()
    mavenLocal()

    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    // Discord Commandler
    implementation "com.elypia.cmdlrdiscord:core:2.0.0"

    // Elypiai
    implementation "com.elypia.elypiai:cleverbot:2.2.1"
    implementation "com.elypia.elypiai:nanowrimo:2.2.1"
    implementation "com.elypia.elypiai:osu:2.2.1"
    implementation "com.elypia.elypiai:path-of-exile:2.2.1"
    implementation "com.elypia.elypiai:runescape:2.2.1"
    implementation "com.elypia.elypiai:steam:2.2.1"
    implementation "com.elypia.elypiai:twitch:2.2.1"
    implementation "com.elypia.elypiai:urbandictionary:2.2.1"
    implementation "com.elypia.elypiai:yugioh-prices:2.2.1"

    implementation "com.elypia.elypiai:extensions-redis:2.2.1"

    // Apache Commons
    implementation "commons-cli:commons-cli:1.4"
    implementation "org.apache.commons:commons-collections4:4.4"

    // Database
    implementation "org.hibernate:hibernate-core:5.4.3.Final"
    runtimeOnly "mysql:mysql-connector-java:8.0.16"

    // Config
    implementation "com.electronwill.night-config:core:3.6.0"
    runtimeOnly "com.electronwill.night-config:toml:3.6.0"

    // Discord
    implementation "net.dv8tion:JDA:4.BETA.0_19"
    implementation "com.sedmelluq:lavaplayer:1.3.17"
    implementation "com.github.DiscordBotList:DBL-Java-Library:2.0.1"

    // Google
    implementation "com.google.cloud:google-cloud-translate:1.79.0"
    implementation "com.google.apis:google-api-services-youtube:v3-rev212-1.25.0"

    // Logging
    implementation "org.slf4j:slf4j-api:1.7.26"
    runtimeOnly "ch.qos.logback:logback-classic:1.2.3"

    // Testing
    testImplementation "org.junit.jupiter:junit-jupiter:5.4.2"
    testImplementation "org.mockito:mockito-core:3.0.0"
}

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

shadowJar {
    manifest {
        attributes 'Implementation-Title' : this.name,
                   'Implementation-Version' : version,
                   'Main-Class' : "com.elypia.alexis.Alexis"
    }
}

/** Testing */

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport

    reports {
        html.enabled = false
    }
}

/** Coverage **/

jacoco {
    toolVersion = "0.8.4"
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = false
    }
}
