plugins {
    id 'java'
    id 'idea'
    id "jacoco"
    id 'com.github.johnrengelman.shadow' version "4.0.3"
}

configurations {
    codacy
}

ext {
    alexisVersion = "3.0.0"
    elypiaiVersion = "2.1.2"
    morphiaVersion = "1.4.0"
    junitVersion = "5.3.2"
}

shadowJar {
    baseName = this.name
    version = alexisVersion

    manifest {
        attributes 'Implementation-Title' : baseName,
                   'Implementation-Version' : version,
                   'Main-Class' : "com.elypia.alexis.Alexis"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

repositories  {
    jcenter()
    mavenCentral()

    maven {
        url 'https://jitpack.io'
    }

    maven {
        url "http://dl.bintray.com/typesafe/maven-releases"
    }
}

dependencies {
    // Elypia
    implementation "com.elypia.elypiai:amazon-product-api:${elypiaiVersion}"
    implementation "com.elypia.elypiai:cleverbot:${elypiaiVersion}"
    implementation "com.elypia.elypiai:nanowrimo:${elypiaiVersion}"
    implementation "com.elypia.elypiai:osu:${elypiaiVersion}"
    implementation "com.elypia.elypiai:runescape:${elypiaiVersion}"
    implementation "com.elypia.elypiai:steam:${elypiaiVersion}"
    implementation "com.elypia.elypiai:twitch:${elypiaiVersion}"
    implementation "com.elypia.elypiai:urbandictionary:${elypiaiVersion}"

    implementation "com.elypia:commandler:1.1.12"
//    implementation "com.elypia.commandler:jda:2.0.0"
    implementation "com.elypia.elyscript:sheets:1.0.1"

    // Config
    implementation "com.electronwill.night-config:toml:3.4.2"

    // Discord
    implementation "net.dv8tion:JDA:3.8.1_447"
    implementation "com.sedmelluq:lavaplayer:1.3.10"
    implementation "com.github.DiscordBotList:DBL-Java-Library:2.0.1"

    // MongoDB / Morphia
    implementation "xyz.morphia.morphia:core:${morphiaVersion}"
    runtimeOnly "xyz.morphia.morphia:logging-slf4j:${morphiaVersion}"

    // Google
    implementation "com.google.cloud:google-cloud-translate:1.53.0"
    implementation "com.google.apis:google-api-services-youtube:v3-rev206-1.25.0"

    // Logging
    implementation "org.slf4j:slf4j-api:1.7.25"
    implementation "ch.qos.logback:logback-classic:1.2.3"

    // Testing
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testRuntimeOnly "org.junit.platform:junit-platform-runner:1.3.2"

    // Mocking
    testImplementation "org.mockito:mockito-core:2.23.4"

    // Codacy
    codacy 'com.github.codacy:codacy-coverage-reporter:-SNAPSHOT'
}

/**
 * Testing, reporting and coverage for Codacy.
 */

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport

    reports {
        html.enabled = false
    }
}

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    reports {
        html.enabled = false
        xml.enabled = true
    }
}

task codacyCoverage(type: JavaExec, dependsOn: jacocoTestReport) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy

    args = [
        "report", "-l", "Java", "-r", "${buildDir}/reports/jacoco/jacocoTestReport/jacoco.xml"
    ]
}
